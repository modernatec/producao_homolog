package br.com.moderna.pnld.view 
{
	/**
	 * ...
	 * @author Roberto Ono
	 */
	import br.com.moderna.pnld.controller.events.aba.AbaSelectEvent;
	import br.com.moderna.pnld.controller.events.control.ChangeVolumeEvent;
	import br.com.moderna.pnld.controller.events.control.ScrollBarEvent;
	import br.com.moderna.pnld.controller.events.capitulo.ControlAtividadesEvent;
	import br.com.moderna.pnld.controller.events.creditos.SetCreditoEvent;
	import br.com.moderna.pnld.controller.SoundController;
	import br.com.moderna.tecnologia.controller.events.preload.PreloadContentEvent;
	import br.com.moderna.tecnologia.model.MediaDTO;
	import caurina.transitions.Tweener; 
	import flash.display.Bitmap;
	import flash.display.BlendMode;
	import flash.display.Loader;
	import flash.display.MovieClip;
	import flash.display.Sprite;
	import flash.events.MouseEvent;
	import flash.events.Event;
	import flash.events.TimerEvent;
	import flash.filters.DropShadowFilter;
	import flash.geom.ColorTransform;
	import flash.geom.Rectangle;
	import flash.system.ApplicationDomain;
	import flash.text.TextFieldAutoSize;
	import flash.ui.Mouse;
	import flash.utils.Timer;
	import flash.utils.getQualifiedClassName;
	import flash.system.fscommand;
	import flash.system.Capabilities;
	 
	public class Control extends MovieClip
	{
		private var main:MovieClip;
		private var soundTimer:Timer;
		private var hideBarDelay:Timer;
		private var downBarDelay:Timer;
		
		private var somBar:MovieClip;
		private var atVoltar:MovieClip;
		private var atAvancar:MovieClip;
		private var openBar:MovieClip;
		private var barraAnos:MovieClip;
		
		private var icoHolderMask:MovieClip;
		private var scrollGap:Number;
		private var scrollMin:Number;
		private var scrollMax:Number;
		private var thumbGap:Number		= 5;
		private var _carregados:Number 	= 0;
		private var _x:Number			= 0;
		private var toolTipController:Tooltip;
		private var icoHolderLastPos:Number;
		private var _showingBoard:Boolean = false;

		public var lastBarY:Number		= 0;		
		public var finished:Boolean		= false;
		public var pointHolder:MovieClip;
		public var Play:MovieClip;
		public var Pause:MovieClip;
		public var info:MovieClip;
		public var soundController:SoundController;
		public var icoHolder:MovieClip;
		public var board:MovieClip;
		public var nextCap:MovieClip;
		public var prevCap:MovieClip;
		public var next:MovieClip;
		public var prev:MovieClip
		public var tituloAnima:MovieClip;
		public var som:MovieClip;
		public var som_on:MovieClip;
		public var som_off:MovieClip;
		public var rever:MovieClip;
		public var closeInfo:MovieClip;
		public var capition_on:MovieClip;
		public var capition_off:MovieClip;
		public var globe:MovieClip;
		public var help:MovieClip;
		
		public var galleryTab:MovieClip;
		public var _paused:Boolean			= false;
		public var galeria:XMLList ;
		public var efemerideAtual:Number 	= 0;
		public var efemerides:Array 		= new Array();
		public var captionBox:MovieClip;
		public var degradeGallery:MovieClip;
		public var mediaHolder:MovieClip;
		public var Lettering:MovieClip;
		public var tituloAnimaBg:MovieClip;
		
		public var delayLettering:Number = 0;
		public var frameStarted:Number = 0;
		public var interactive:Boolean = false;
		public var logo:MovieClip;

		public function Control() 
		{			
			addEventListener(Event.ADDED_TO_STAGE, this.init);					
		}
		
		public function init(e:Event):void {
			this.main			= loaderInfo.loader.content as MovieClip;

			this.toolTipController			= new Tooltip();
			this.toolTipController.color 	= this.main.infoXML.setup.colors.firstColor;
			stage.addEventListener(MouseEvent.MOUSE_UP, this.stopScrubber);
			
			var buttons:XMLList			= this.main.infoXML.skin.buttons;
			for (var i:Number = 0; i < buttons.children().length(); i++) {
				var obj:Object 			= buttons.children()[i];
				
				var classBtn:Class		= loaderInfo.applicationDomain.getDefinition(obj.name()) as Class;
				if (classBtn) {
					var mc:MovieClip 	= new classBtn();
					mc.x 				= obj.@x;
					mc.y				= obj.@y;
					mc.tooltip 			= obj.@tooltip;
					mc.visible			= (obj.@visible != undefined) ? obj.@visible as Boolean : true;
					/*
					 * Fluxo para a barra preta do tituloAnimaBg
					 */					
					mc.showBg = (obj.@showBg != undefined) ? obj.@showBg : true;					
					if (mc.bg) {
						var cor:ColorTransform	= mc.bg.transform.colorTransform;
						cor.color				= Number("0x" + this.main.infoXML.setup.colors.firstColor);
						mc.bg.transform.colorTransform 	= cor;
					}
					
					if (obj.@tooltip != undefined) {
						mc.addEventListener(MouseEvent.MOUSE_OVER, this.setTooltip);
						mc.addEventListener(MouseEvent.MOUSE_OUT, this.resetTooltip);
						mc.mouseChildren 	= false;
						mc.buttonMode		= true;
					}
					
					this.check(obj.name(), mc);
					(obj.@fixed != undefined) ? this.main.animaHolder.addChild(mc) : addChild(mc);					
				}
			}
		
			
			if (!this.soundTimer){
				this.soundTimer			= new Timer(1000, 1);
				this.soundTimer.addEventListener(TimerEvent.TIMER_COMPLETE, this.hideSoundBar);
				this.soundController	= new SoundController();
				addChild(this.toolTipController);	
			}
			
			if (!this.hideBarDelay){
				this.hideBarDelay		= new Timer(this.main.infoXML.setup.hideBarDelay * 1000, 1);
				this.hideBarDelay.addEventListener(TimerEvent.TIMER_COMPLETE, this.hideBar);
				this.hideBarDelay.start();
			}
			
			if (!this.downBarDelay){
				this.downBarDelay		= new Timer(this.main.infoXML.setup.downBarDelay * 1000, 1);
				this.downBarDelay.addEventListener(TimerEvent.TIMER_COMPLETE, this.downBar);
				this.downBarDelay.start();
			}
			
			this.loadIcon();
		}
		
		private function check(name:String, mc:MovieClip):void {
			switch(name) {
				case "logo":
					this.logo	= mc;
				break;
				case "Play":
					mc.addEventListener(MouseEvent.CLICK, this._play);
					mc.visible	= false;
					this.Play	= mc;
				break;
				case "Pause":
					mc.addEventListener(MouseEvent.CLICK, this._pause);
					this.Pause	= mc;
				break;
				case "som":		
					mc.addEventListener(MouseEvent.MOUSE_OVER, this.showSoundBar);
					mc.addEventListener(MouseEvent.MOUSE_OUT, this.setTimer);
					mc.addEventListener(MouseEvent.CLICK, this.setVolume);
					this.som	= mc;
				break;
				case "som_on":
					mc.addEventListener(MouseEvent.CLICK, this.switchVolume);
					this.som_on	= mc;
				break;
				case "som_off":
					mc.addEventListener(MouseEvent.CLICK, this.switchVolume);
					this.som_off	= mc;
				break;
				case "somBar":
					mc.scrubber.buttonMode		= true;
					mc.scrubber.addEventListener(MouseEvent.MOUSE_DOWN, this.moveScrubber);
					mc.addEventListener(MouseEvent.MOUSE_OVER, this.stopTimer);
					mc.addEventListener(MouseEvent.MOUSE_OUT, this.setTimer);
					this.somBar	= mc;
				break;
				case "prev":
					this.prev = mc;
					mc.addEventListener(MouseEvent.CLICK, this.prevSession);
				break;
				case "next":
					this.next = mc;
					mc.addEventListener(MouseEvent.CLICK, this.nextSession);
				break;
				case "prevCap":
					mc.addEventListener(MouseEvent.CLICK, this.scrollBarRight);
					mc.visible				= false;
					this.prevCap			= mc;
					this.icoHolder			= new MovieClip();
					this.icoHolder.x		= mc.x + mc.width + this.thumbGap;
					this.icoHolder.y		= mc.y;
					this.icoHolder.alpha	= 0;
					this.icoHolder.visible	= false;
					this.icoHolderLastPos	= this.icoHolder.x;
					addChild(this.icoHolder);
				break;
				case "openBar":
					mc.addEventListener(MouseEvent.CLICK, this.showChaptersBar);
					this.openBar	= mc;
				break;
				case "nextCap":
					mc.addEventListener(MouseEvent.CLICK, this.scrollBarLeft);
					mc.visible		= false;
					this.nextCap	= mc;
				break;
				case "atVoltar":
					mc.addEventListener(MouseEvent.CLICK, this.voltarAt);
				break;
				case "atAvancar":
					mc.addEventListener(MouseEvent.CLICK, this.avancarAt);
				break;	
				case "info":
					mc.addEventListener(MouseEvent.MOUSE_OVER, this.showTooltipIco);
					mc.addEventListener(MouseEvent.MOUSE_OUT, this.hideTooltipIco);
					mc.addEventListener(MouseEvent.CLICK, this.showBoard);
					mc.mouseChildren 	= false;
					mc.buttonMode		= true;
					this.info			= mc;
				break;
				case "infoBoard":
					this.board					= mc;
					mc.content.texto.autoSize 	= TextFieldAutoSize.LEFT;
					mc.content.texto.htmlText 	= this.main.creditosEditora;
					
					mc.fechar.mouseChildren		= false;
					mc.fechar.buttonMode		= true;
					mc.fechar.addEventListener(MouseEvent.CLICK, this.hideBoard);
					mc.fechar.addEventListener(MouseEvent.MOUSE_OVER, this.showTooltipIco);
					mc.fechar.addEventListener(MouseEvent.MOUSE_OUT, this.hideTooltipIco);
					
					var scrollClass:Class		= loaderInfo.applicationDomain.getDefinition("Scrollbar") as Class;
					var scrollBar				= new scrollClass( { scroll:mc, timer:1 } );
					scrollBar.x					= mc.content.x + mc.content.width + 10;
					scrollBar.y					= mc.content.y + 14;
					mc.addChild(scrollBar);	
				break;
				case "rever":
					this.rever			= mc;
					mc.addEventListener(MouseEvent.CLICK, this.reverAnima);
					mc.mouseChildren 	= false;
					mc.buttonMode		= true;
					mc.visible			= false;
					mc.alpha			= 0;
				break;
				case "help":
					var media:MediaDTO 		= new MediaDTO();
					media.titulo			= this.main.infoXML.setup.tutorial.title;
					media.tipo				= this.main.infoXML.setup.tutorial.@type;
					media.objDisplayUri		= this.main.infoXML.setup.tutorial.img;
					media.objUri			= this.main.infoXML.setup.tutorial.file;

					mc.media				= media;
					mc.addEventListener(MouseEvent.MOUSE_OVER, this.showTooltipIco);
					mc.addEventListener(MouseEvent.MOUSE_OUT, this.hideTooltipIco);
					mc.addEventListener(MouseEvent.CLICK, this.showTutorial);
					mc.mouseChildren 	= false;
					mc.buttonMode		= true;
					this.help			= mc;
				break;
				case "galleryTab":
					var sq:MovieClip		= new MovieClip();
					sq.mouseEnabled			= false;
					sq.alpha				= 0;
					sq.graphics.beginFill(0x000000);
					sq.graphics.drawRect(0, 0, this.main.infoXML.setup.width, this.main.infoXML.setup.height);
					sq.graphics.endFill();
					this.main.animaHolder.addChild(sq);
					this.degradeGallery 	= sq;
				
					this.galleryTab			= mc;
					mc.status 				= false;
					mc.orign 				= mc.x;
					mc.fechar.mouseChildren = false;
					mc.fechar.buttonMode	= true;
					mc.fechar.addEventListener(MouseEvent.CLICK, this.switchAbaLateral);
					
					var cor:ColorTransform	= mc.tab.holder.bg.transform.colorTransform;
					cor.color				= Number("0x" + this.main.infoXML.setup.colors.firstColor);
					mc.tab.holder.bg.transform.colorTransform 	= cor;
						
					var cor2:ColorTransform	= mc.tab.btn.transform.colorTransform;
					cor2.color				= Number("0x" + this.main.infoXML.setup.colors.firstColor);
					mc.tab.btn.transform.colorTransform 	= cor2;

					this.loadGaleria();
				break;
				case "closeInfo":
					mc.mouseChildren	= false;
					mc.buttonMode		= true;
					mc.addEventListener(MouseEvent.CLICK, this.closeAnima);
					mc.addEventListener(MouseEvent.MOUSE_OVER, this.showTooltipIco);
					mc.addEventListener(MouseEvent.MOUSE_OUT, this.hideTooltipIco);
					this.closeInfo 		= mc;
					this.checkCapabilities();
				break;
				case "tituloAnima":	
					this.tituloAnima 			= mc;					
					mc.txt_titulo_info.autoSize	= TextFieldAutoSize.LEFT;
					if(this.main.infoXML.intro.title.@multiline != undefined)
						mc.txt_titulo_info.multiline = true;
					
					mc.txt_titulo_info.htmlText = this.main.infoXML.intro.title;
					
					if (this.tituloAnimaBg) {
						if(mc.showBg==true){
							/*
							 * Para aparecer a barra preta na animação 
							 */
							this.tituloAnimaBg.width = this.main.infoXML.setup.width;
							this.tituloAnimaBg.height = mc.y + (mc.txt_titulo_info.height + 15);
						}else{
							/*
							 * Para não aparecer a barra
							 */ 
							this.tituloAnimaBg.width = 0;
							this.tituloAnimaBg.height = 0;
						}
					}

				break;
				case "tituloAnimaBg":
					this.tituloAnimaBg = mc;
				break;
				case "caption_on":
					this.capition_on = mc;
					mc.addEventListener(MouseEvent.CLICK, this.switchCaption);
					this.setCaptionBox();
				break;
				case "caption_off":
					this.capition_off = mc;
					mc.addEventListener(MouseEvent.CLICK, this.switchCaption);
				break;
				case "Lettering":
					this.Lettering						= mc;
					this.Lettering.alpha				= 0;
					this.Lettering.mouseChildren 		= false;
					this.Lettering.lettering.multiline	= true;
					this.Lettering.lettering.autoSize	= TextFieldAutoSize.LEFT;
				break;
			}
		}
		
		private function closeAnima(evt:MouseEvent):void {
			fscommand("quit");
		}
		
		private function checkCapabilities():void {
			switch (Capabilities.playerType) {
				case 'External':					
				break;
				case 'PlugIn':
				case 'ActiveX':
					//browser
				break;
				default:
					this.closeInfo.visible = true;
				
			}			
		}
		
		public function setLettering(e:SetCreditoEvent):void {
			this.Lettering.y 					= (this.main.capHolder.y > 0 ) ? this.main.capHolder.y : this.main.capHolder.y + this.tituloAnima.height;
			this.Lettering.lettering.htmlText	= e.credito.credito;
			Tweener.removeTweens(this.Lettering);
			this.frameStarted   				= this.main._capituloAtualMc.currentFrame;
			this.delayLettering 				= e.credito.time;

			Tweener.addTween(this.Lettering, {alpha:1, time:0.8, transition:"linear", onComplete:resetLettering, onCompleteParams:[e.credito.time]} );
		}
		
		
		public function resetLettering(delay:Number):void {
			if (delay)
				Tweener.addTween(this.Lettering, { alpha:0, time:1, delay:delay, transition:"linear", onComplete:function() { delayLettering = 0; frameStarted = 0 }} );	
		}
		
		public final function pauseLettering():void {
			if (Tweener.isTweening(this.Lettering) && this.Lettering)
				Tweener.pauseTweens(this.Lettering);
		}
		public final function resumeLettering():void {
			if(this.Lettering)
				Tweener.resumeTweens(this.Lettering);
		}
		
		
		public function switchAbaLateral(e:MouseEvent = null):void {
			var _x:Number;
			this.hideBoard(e);
			if (this.galleryTab && !this.galleryTab.status) {
				if(!this._paused && !this.finished)
					this._pause(e);
					
				this.galleryTab.status 	= true;
				
				_x = this.galleryTab.orign - (this.galleryTab.width - this.galleryTab.fechar.width);
				
				if (this.degradeGallery) {
					Tweener.addTween(this.degradeGallery, { alpha:.7, time:.5, transition:"linear" } );
				}
				
				Tweener.addTween(this.galleryTab.fechar.seta, {alpha:.5,  _color:0x000000, rotation:0, time:0.8 } );
				Tweener.addTween(this.galleryTab, { x:_x, time:.5, transition:"easeOutQuad" } );
				Tweener.addTween(this.main._capituloAtualMc, { _brightness:-.5, alpha: .8, time:1, transition:"linear" } );
				Tweener.addTween(this.barraAnos, {_brightness:-1, time:1, transition:"linear" } );
			}else {
				this.closeAbaLateral(e);
				if(!this._paused && !this.finished)
					this._play(e);
			}
			
		}
		
		public function closeAbaLateral(e:MouseEvent = null):void {
			this.galleryTab.status 	= false;
			_x = this.galleryTab.orign;
			
			if (this.degradeGallery) {
				Tweener.addTween(this.degradeGallery, { alpha:0, time:.5, transition:"linear", onComplete:function() {
					this.mouseEnabled = false;
					this.mouseChildren = false;
					} } );
			}
			Tweener.addTween(this.galleryTab.fechar.seta, { _color:null, rotation:180, time:0.8 } );
			Tweener.addTween(this.galleryTab, { x:_x, time:.5, transition:"easeOutQuad" } );
			Tweener.addTween(this.main._capituloAtualMc, { _brightness:0, alpha:1, time:1, transition:"linear" } );
			Tweener.addTween(this.barraAnos, {_brightness:0, time:1, transition:"linear" } );
		}
		
		public function showTutorial(e:MouseEvent):void {
			this._pause(e);
			this.mediaItem(e);
		}
		
		private function loadGaleria():void {
			this.galeria		 		= this.main.infoXML.tabs.children()[this.main.abaSelecionada].gallery;
			var opcaoClass:Class		= loaderInfo.applicationDomain.getDefinition("opcao") as Class;
			var item:MovieClip;
			var _width:Number			= 0;
			var _height:Number			= 5;
			var ptClass:Class			= loaderInfo.applicationDomain.getDefinition("panelTitulo") as Class;
			
			if(ptClass){
				var pt:MovieClip		= new ptClass();
				pt.titulo.autoSize		= TextFieldAutoSize.LEFT;
				pt.titulo.multiline		= true;
				pt.titulo.htmlText		= galeria.title;				
				pt.id 					= String(galeria.title);
				pt.status 				= true;
				pt.x 					= this.galleryTab.tab.holder.x + 5;
				pt.alpha				= .6;
				
				_width					= (pt.width > _width) ? pt.width + 15 : _width;
				_height					+= pt.height;
				this.galleryTab.tab.addChild(pt);
			}
			
			if (opcaoClass) {
				for (var i:Number = 0; i < galeria.itens.children().length(); i++ ) {
					item = new opcaoClass();
					var ico:MovieClip;
					
					if (galeria.itens.children()[i].color != undefined) {
						ico	= new MovieClip();
						ico.graphics.lineStyle(1, 0xFFFFFF);
						ico.graphics.beginFill(Number("0x" + galeria.itens.children()[i].color));
						ico.graphics.drawRect(0, 3, 31, 14);
						ico.graphics.endFill();	
					}

					if (galeria.itens.children()[i].ico != undefined) {
						var icoClass:Class	= loaderInfo.applicationDomain.getDefinition(String(galeria.itens.children()[i].ico)) as Class;
						if (icoClass) {
							ico	= new icoClass();
						}
					}
					
					ico.alpha			= .6;	
					ico.cacheAsBitmap 	= true;
					
					item.addChild(ico);	
					item.ico 				= ico;
					item.id 				= i;
					
					var media:MediaDTO 		= new MediaDTO();
					media.titulo			= galeria.itens.children()[i].media.title;
					media.credito			= galeria.itens.children()[i].media.credit
					media.descricao			= galeria.itens.children()[i].media.description;					
					media.tipo				= galeria.itens.children()[i].media.@type;
					media.width				= galeria.itens.children()[i].media.@width;
					media.height			= galeria.itens.children()[i].media.@height;
					media.objDisplayUri		= galeria.itens.children()[i].media.img;
					media.objUri			= galeria.itens.children()[i].media.file;

					item.media				= media;
					item.status 			= false;

					item.mc.x 				= ico.width + 2;
					item.mc.blendMode		= BlendMode.MULTIPLY;
					item.mc.opcao.autoSize	= TextFieldAutoSize.LEFT;
					item.mc.opcao.multiline	= true;					
					item.mc.opcao.htmlText	= galeria.itens.children()[i].label;
					item.mouseChildren		= false;
					item.buttonMode			= true;
					item.addEventListener(MouseEvent.CLICK, this.mediaItem);	
					
					item.x 					= this.galleryTab.tab.holder.x + 10;
					item.y 					= _height;
					item.alpha				= .5;

					_width					= (item.width > _width) ? item.width + 20 : _width;
					_height					+= item.height + 5;
					this.galleryTab.tab.addChild(item);
				}
				
			}
			
			this.galleryTab.tab.holder.width 	= _width;
			this.galleryTab.tab.holder.height 	= _height;
		}
		
		private function mediaItem(e:MouseEvent):void {
			this._pause(e);
			Tweener.addTween(this.degradeGallery, { alpha:1, time:.5, transition:"linear" } );
			this.galleryTab.visible	= false;
			this.mediaHolder 		= new MovieClip();
			this.main.animaHolder.addChild(this.mediaHolder);
			
			this.main.setMedia(e.target.media);
			var player:MovieClip = this.main.mediaPlayer;				
			
			if(e.target.media.width != 0)
				player.width = e.target.media.width;
			
			if(e.target.media.height != 0)
				player.height = e.target.media.height;
				
			var btnVoltarClass	= loaderInfo.applicationDomain.getDefinition("voltar") as Class;
			if (btnVoltarClass) {
				var voltar:MovieClip	= new btnVoltarClass();
				voltar.x				= (player.x + player.width) - voltar.width;
				voltar.y				= player.y - voltar.height;
				voltar.mouseChildren	= false;
				voltar.buttonMode		= true;
				
				voltar.addEventListener(MouseEvent.CLICK, closeMedia);
				
				this.mediaHolder.addChild(voltar);
			}
			
			var tituloClass	= loaderInfo.applicationDomain.getDefinition("tituloMedia") as Class;
			if (tituloClass && e.target.media.titulo) {
				var titulo:MovieClip	= new tituloClass();
				titulo.x				= player.x;
				titulo.y				= player.y - titulo.height;
				titulo.titulo.autoSize	= TextFieldAutoSize.LEFT;
				titulo.titulo.htmlText	= e.target.media.titulo;
				
				this.mediaHolder.addChild(titulo);
			}
			
			var creditoClass				= loaderInfo.applicationDomain.getDefinition("creditoMedia") as Class;
			if (creditoClass && e.target.media.credito) {
				var credito:MovieClip		= new creditoClass();
				credito.x					= player.x + player.width;
				credito.y					= player.y;
				Tweener.addTween(credito.credito, {_text_letterSpacing:1, time:0} );
				credito.credito.autoSize	= TextFieldAutoSize.LEFT;
				credito.credito.htmlText	= e.target.media.credito;
				
				this.mediaHolder.addChild(credito);
			}
			
			var descricaoClass				= loaderInfo.applicationDomain.getDefinition("descricaoMedia") as Class;
			if (descricaoClass && e.target.media.descricao) {
				var descricao:MovieClip		= new descricaoClass();
				descricao.x					= player.x;
				descricao.y					= player.y + player.height;

				descricao.content.texto.autoSize	= TextFieldAutoSize.LEFT;
				descricao.content.texto.multiline	= true;
				descricao.content.texto.htmlText	= e.target.media.descricao;
				
				
				var scrollClass:Class			= loaderInfo.applicationDomain.getDefinition("Scrollbar") as Class;
				var scrollBar					= new scrollClass( { scroll:descricao, timer:.5 } );
				descricao.content_mask.width	= player.width - scrollBar.width;
				scrollBar.x						= player.width - scrollBar.width;
				scrollBar.y						= descricao.content.y + 14;
				descricao.addChild(scrollBar);	
				
				this.mediaHolder.addChild(descricao);
			}
		}
		
		private function closeMedia(e:MouseEvent):void {
			if (!this.galleryTab)
				return

			this.main.animaHolder.removeChild(this.mediaHolder);
			Tweener.addTween(this.degradeGallery, { alpha:.7, time:.5, transition:"linear" } );
			
			this.galleryTab.visible	= true;
			
			if (this.main.mediaPlayer && this.main.contains(this.main.mediaPlayer)){
				try {
					this.main.mediaPlayer.closeVideo();
				}catch (e:Error) {
					try {
						this.main.mediaPlayer.stopSound();
					}catch (e:Error) {}
				}
				this.main.removeChild(this.main.mediaPlayer);
			}

			if (!this.galleryTab.status && !this._paused && !this.finished)
				this._play(e);	
		}

		private function reverAnima(e:MouseEvent):void {
			e.target.visible 	= false;
			e.target.alpha		= 0;
			this._paused		= false;
			this.Play.mouseEnabled	= true;
			this.next.mouseEnabled	= true;
			this.main.setAbaSelecionada(new AbaSelectEvent(AbaSelectEvent.ABA_SELECT, this.main.abaSelecionada));
		}
		
		/**
		 * Funcoes referentes ao texto mostrando a transcricao do loc
		 * Botoes: caption, caption_off
		 */
		public function switchCaption(e:MouseEvent = null):void {
			if (this.main.capHolder.scaleY == 1) {
				this.capition_on.visible	= true;
				this.capition_off.visible	= false;
								
				this.main.container.height -= 10;
				(this.main.container.scaleX < this.main.container.scaleY ) ? this.main.container.scaleY = this.main.container.scaleX : this.main.container.scaleX = this.main.container.scaleY
				
				var _x:Number	= (this.main.infoXML.setup.width - this.main.container.width) / 2;
				Tweener.addTween(this.main.capHolder, { x:_x, scaleY:this.main.container.scaleY, scaleX:this.main.container.scaleX, time:.5, transition:"linear", onComplete:function() {				
					Tweener.addTween(main.maskCap, { height:main.container.height, time:.1, transition:"linear",onComplete:function(){showCaptionBox();} } );
					}} );
					
				Tweener.addTween(this.main.creditoController.holderLegenda, { y: -50, time:.5, transition:"linear" } );
				
				Tweener.addTween(this.main.control, { y: -50, time:.5, transition:"linear", onComplete:function() {
						lastBarY = this.y;
						main.creditoController.holderLegenda.y = lastBarY;
					}  } );
			}else {
				this.resetCaptionBox();
				this.capition_on.visible	= false;
				this.capition_off.visible	= true;
				
				this.main.container.scaleX 	= this.main.containerScaleX;
				this.main.container.scaleY 	= this.main.containerScaleY;
				
				Tweener.addTween(this.main.capHolder, { x:0, scaleX:1, scaleY:1, time:.5, transition:"linear"} );
				Tweener.addTween(this.main.maskCap, { scaleX:1, scaleY:1, time:.5, transition:"linear"})
				Tweener.addTween(this.main.creditoController.holderLegenda, { y:0, time:.5, transition:"linear" } );
				Tweener.addTween(this.main.control, { y:0, time:.5, transition:"linear", onComplete:function() {
						lastBarY = this.y;
						main.creditoController.holderLegenda.y = lastBarY;
					} } );
			}
		}
		
		public function setCaptionBox():void {
			var descricaoClass			= loaderInfo.applicationDomain.getDefinition("descricaoMedia") as Class;
			if (!descricaoClass)
				return;
				
			var captionBox:MovieClip	= new MovieClip();
			this.captionBox				= captionBox;
			captionBox.alpha			= 0;
			captionBox.visible			= false;
			
			var bg:MovieClip			= new MovieClip();
			bg.name						= "bg";
			captionBox.addChild(bg);
			
			var descricao:MovieClip		= new descricaoClass();
			descricao.name 				= "descricao";
			descricao.x					= 5;
			descricao.y					= 3;

			descricao.content.texto.autoSize	= TextFieldAutoSize.LEFT;
			descricao.content.texto.multiline	= true;
			descricao.content.texto.htmlText	= "";
			
			descricao.content_mask.width		= Math.ceil(this.main.infoXML.setup.width - descricao.x);
			descricao.content.texto.width		= Math.ceil(this.main.infoXML.setup.width - descricao.x);
			
			var scrollClass:Class		= loaderInfo.applicationDomain.getDefinition("Scrollbar") as Class;
			var scrollBar				= new scrollClass( { scroll:descricao, timer:.5 } );
			descricao.addChild(scrollBar);
			captionBox.addChild(descricao);			
			this.main.animaHolder.addChild(captionBox);
		}
		
		public function showCaptionBox():void {
			
			var bg:MovieClip	= this.captionBox.getChildByName("bg") as MovieClip;
			bg.graphics.beginFill(0x000000);
			bg.graphics.drawRect(0, 0, this.main.infoXML.setup.width, this.main.infoXML.setup.height - this.main.maskCap.height);
			bg.graphics.endFill();
			this.captionBox.y 		= this.main.maskCap.height;
			
			this.captionBox.visible = true;
			Tweener.addTween(this.captionBox, { alpha:1, time:.5, transition:"linear" } );
		}
		
		public function resetCaptionBox():void {
			this.captionBox.visible = false;
			this.captionBox.alpha	= 0;
		}
		
		public function setCaption(text:String):void {
			if (!this.captionBox)
				return;
				
			var caption:MovieClip = this.captionBox.getChildByName("descricao") as MovieClip;
			if (caption)
				caption.content.texto.htmlText = text;
		}
		
		
		/**
		 * Funcoes para criar box interativo
		 * 
		 */
		public function setBox(e:MouseEvent):void {
			this.resetBox(e);
			this.hideBoard();
			
			if (this.galleryTab && this.galleryTab.status)
				this.closeAbaLateral();	

			var destaques:XMLList	= this.main.infoXML.tabs.children()[this.main.abaSelecionada].chapters.children()[this.main._capituloAtual].interactive.boxes.children();
			var boxId:String		= (e.type == MouseEvent.MOUSE_OVER) ? e.target.tooltip : e.target.name; 
			var destaque:XMLList	= destaques.(@id == boxId);
			
			if (destaque.toString() != "") {	
				var holder:MovieClip		= new MovieClip();
				holder.name 				= "interactiveBox";
				
				var iBox:MovieClip			= new MovieClip();
				iBox.name					= "iBox";
				holder.addChild(iBox);
				this.main._capituloAtualMc.addChild(holder);
				
				if (destaque.@img != undefined) {
					var imgHolder:MovieClip	= new MovieClip();
					imgHolder.name			= "imgHolder";
					iBox.addChild(imgHolder);
					
					var media:MediaDTO		= new MediaDTO();
					media.objUri			= destaque.@img;
					media.holder			= imgHolder;
					media.showPreload		= true;
					media.callBack			= this.imgBoxComplete;					
					media.params			= [e, destaque];
					
					if (destaque.@width != undefined)
						media.width			= destaque.@width;
					
					if (destaque.@height != undefined)
						media.height		= destaque.@height;
						
					this.main.parent.dispatchEvent(new PreloadContentEvent(PreloadContentEvent.PRELOAD_CONTENT, media));
				}else {
					this.setLayoutBox([e, destaque]);
				}
			}
		}
		
		public function imgBoxComplete(m:MediaDTO):void {
			if (m.holder.width > m.width) {
				m.holder.width 	= m.width;
				m.holder.scaleY = m.holder.scaleX;
			}
			
			if (m.holder.height > m.height) {
				m.holder.height = m.height;
				m.holder.scaleX = m.holder.scaleY;
			}	
			
			this.main.checkPreload(false);
			this.setLayoutBox(m.params);
		}
		
		public function setLayoutBox(params:Array):void {
			var descricaoClass		= loaderInfo.applicationDomain.getDefinition("boxDestaque") as Class;
			var holder:MovieClip	= this.main._capituloAtualMc.getChildByName("interactiveBox") as MovieClip;
			var iBox:MovieClip		= holder.getChildByName("iBox") as MovieClip;
			if (!descricaoClass && !holder && !iBox)
				return;
				
			var e:MouseEvent			= params[0];
			var destaque:XMLList		= params[1];
			var dropShadow:DropShadowFilter = new DropShadowFilter(4, 45, 0, .7);
			
			var descricao:MovieClip		= new descricaoClass();
			var boxColor:Number			= (destaque.@color) ? Number("0x" + destaque.@color) : 0x000000;
			var _width:Boolean			= false;
			var _height:Boolean			= false;
			var squareHeight:Number;
			
			iBox.addChild(descricao);
			descricao.content.texto.autoSize	= TextFieldAutoSize.LEFT;
			descricao.content.texto.multiline	= true;
			descricao.content.texto.htmlText	= destaque;

			var img:MovieClip	= iBox.getChildByName("imgHolder") as MovieClip;
			if (img) {					
				img.x			= 10;
				img.y			= 10;
				descricao.x 	= img.x;
				descricao.y 	= img.y + img.height + 5;
				descricao.content.texto.width	= img.width - img.x;
				descricao.content_mask.width	= descricao.content.texto.width;
				descricao.content_mask.height	= (destaque.@height - img.height > 0) ? destaque.@height - img.height : 100;
				
				squareHeight = descricao.y + descricao.content_mask.height;					
			}else {
				descricao.x 	= 10;
				descricao.y 	= 10;
				
				if(destaque.@width != undefined)
					descricao.content.texto.width	= destaque.@width;
				
				if(destaque.@height != undefined)
					descricao.content_mask.height	= destaque.@height;
				
				descricao.content_mask.width		= (destaque.@width != undefined) ? destaque.@width : descricao.content.width;	
				
				if((descricao.content.y + descricao.content.height) < descricao.content_mask.height)
					descricao.content_mask.height	= descricao.content.y + descricao.content.height + descricao.y;
				
				squareHeight = descricao.y + descricao.content_mask.height;								
			}

			var scrollClass:Class			= loaderInfo.applicationDomain.getDefinition("Scrollbar") as Class;
			var scrollBar					= new scrollClass( { scroll:descricao, timer:.5 } );
			scrollBar.x						= descricao.width;
			scrollBar.y						= descricao.content.y + 12;
			descricao.addChild(scrollBar);
			
			iBox.x	= e.target.x + e.target.parent.x + (e.target.width * 3);
			iBox.y	= (destaque.@height != undefined) ? (this.main.infoXML.setup.height / 2) - (squareHeight / 2) : e.target.y + e.target.parent.y - ((iBox.height + e.target.height) / 2);;
			
			var square:Sprite = new Sprite();
			square.graphics.beginFill(boxColor);
			square.graphics.drawRoundRect(0, 0, descricao.width + descricao.x, squareHeight, 10);
			square.graphics.endFill();
			square.x = iBox.x;
			square.y = iBox.y;
			
			if (Number(iBox.x + square.width) > this.main.infoXML.setup.width) {
				iBox.x 		= (e.target.x + e.target.parent.x) - ((e.target.width * 3) + iBox.width);
				square.x 	= iBox.x;
				_width 		= true;
			}else {
				_width = false;
			}
			
			var _x:Number 	= (_width) ? iBox.x + iBox.width : iBox.x;
			var sprite:Sprite 	= new Sprite();
			sprite.graphics.beginFill(boxColor);
			sprite.graphics.moveTo(e.target.x + (e.target.width / 2) + e.target.parent.x, e.target.y + (e.target.height / 2 ) + e.target.parent.y);		
			sprite.graphics.lineTo(_x, iBox.y + (square.height / 4));
			sprite.graphics.lineTo(_x, iBox.y + (square.height / 2));
			sprite.graphics.lineTo(e.target.x + (e.target.width / 2) + e.target.parent.x, e.target.y + (e.target.height / 2 ) + e.target.parent.y);
			sprite.graphics.endFill();
			
			if(img)
				square.width += (img.x * 2);
			
			if (scrollBar.scroller.visible)
				square.width += scrollBar.width;
			
			var fecharClass:Class		= loaderInfo.applicationDomain.getDefinition("fecharBox") as Class;
			if (fecharClass && e.type == MouseEvent.CLICK) {
				var fechar:MovieClip 	= new fecharClass();
				fechar.buttonMode		= true;
				fechar.x 				= iBox.x + descricao.width;
				square.y 				-= fechar.height;
				fechar.y 				= square.y + (fechar.height / 2);
				
				square.height			+= 30;
				if(img){
					square.width		+= descricao.x;
					fechar.x			+= img.x * 2;
				}
				
				fechar.addEventListener(MouseEvent.CLICK, this.resetBox);
				fechar.addEventListener(MouseEvent.MOUSE_OVER, this.showTooltipIco);
				fechar.addEventListener(MouseEvent.MOUSE_OUT, this.hideTooltipIco);
				
				holder.addChild(fechar);
				
				if(e.target.hasEventListener(MouseEvent.MOUSE_OUT))
					e.target.removeEventListener(MouseEvent.MOUSE_OUT, this.resetBox);

			}else {
				if(!e.target.hasEventListener(MouseEvent.MOUSE_OUT))
					e.target.addEventListener(MouseEvent.MOUSE_OUT, this.resetBox);
			}
			
			var box:MovieClip 	= new MovieClip();
			box.alpha 			= .8
			box.filters 		= [dropShadow];
			box.addChild(sprite);
			box.addChild(square);
			
			holder.addChild(box);
			holder.setChildIndex(box, 0);
		}
		
		public function resetBox(e:MouseEvent = null):void {
			var box:MovieClip = this.main._capituloAtualMc.getChildByName("interactiveBox") as MovieClip;
			if (box)
				this.main._capituloAtualMc.removeChild(box);
		}
		
		/**
		 * Funcoes para mostar/esconder o box de credito "i"
		 * Botoes: info
		 */
		public function showBoard(e:MouseEvent):void {
			var cor:ColorTransform	= this.info.mc_i.transform.colorTransform;
			if (this._showingBoard) {
				if(!this._paused && !this.finished)
					this._play(e);
				
				cor 				= new ColorTransform();
				this.board.visible 	= false;
				this._showingBoard	= false;
			}else {
				if (this.galleryTab && this.galleryTab.status)
					this.switchAbaLateral(e);
				
				if(!this._paused && !this.finished)
					this._pause(e);
				
				cor.color			= 0xFFFFFF;
				this.board.visible 	= true;
				this._showingBoard	= true;
			}
			
			this.info.mc_i.transform.colorTransform = cor;
		}
		
		public function hideBoard(e:MouseEvent = null):void {
			if (this._showingBoard) {
				this.info.mc_i.transform.colorTransform = new ColorTransform();
				this.board.visible 	= false;
				this._showingBoard	= false;
				
				if(!this._paused && !this.finished)
					this._play(e);
			}
		}
		
		/**
		 * Funcoes para criar/remover os thumbs de capitulos do info
		 * geralmente usados nas animacoes
		 */		
		public function loadIcon():void {
			if (!this.icoHolder){
				this.main.loadCapitulos(this.main._carregados, true, true);
				return;
			}	
			
			this.removeIcon();
			var capitulos:XMLList = this.main.infoXML.tabs.children()[this.main.abaSelecionada].chapters.children();
			if (this._carregados < capitulos.length()) {
				var holderIco:MovieClip		= new MovieClip();
				holderIco.tooltip 			= capitulos[_carregados].title;
				holderIco.capitulo 			= this._carregados;
				holderIco.alpha				= .5;
				holderIco.mouseChildren		= true;
				holderIco.buttonMode		= true;
				holderIco.addEventListener(MouseEvent.CLICK, this.gotoCapitulo);
				
				this.icoHolder.addChild(holderIco);				
				var layoutObj:MediaDTO		= new MediaDTO();
				layoutObj.objUri			= capitulos[_carregados].@ico;
				layoutObj.holder			= holderIco;
				layoutObj.callBack			= this.iconComplete;
				this.main.parent.dispatchEvent(new PreloadContentEvent(PreloadContentEvent.PRELOAD_CONTENT, layoutObj));
			}else {
				this.scrollMin				= Math.ceil((this.icoHolder.x - (this.icoHolder.width - this.icoHolderMask.width)) - this.thumbGap);
				this.scrollMax				= Math.ceil(this.icoHolder.x);
				this.main.loadCapitulos(this.main._carregados, true, true);
			}
		}
		
		public function iconComplete(m:MediaDTO):void {			
			var child:MovieClip	= this.icoHolder.getChildAt(_carregados) as MovieClip;
			child.x 			= _x;
			child.addEventListener(MouseEvent.MOUSE_OVER, this.setTooltip);
			child.addEventListener(MouseEvent.MOUSE_OUT, this.resetTooltip);
			_x 					+= child.width + this.thumbGap; 
			
			if (!icoHolderMask) {
				this.scrollGap 		= _x;
				this.icoHolderMask	= new MovieClip();
				this.icoHolderMask.graphics.beginFill(0xFF0000);
				this.icoHolderMask.graphics.drawRect(this.icoHolder.x, this.icoHolder.y, _x * this.main.infoXML.setup.visibleIcons, child.height);
				this.icoHolderMask.graphics.endFill();
				addChild(this.icoHolderMask);
				this.icoHolder.mask	= this.icoHolderMask;
				
				if(this.nextCap)
					this.nextCap.x 	= this.icoHolder.x + this.icoHolderMask.width;
			}
			
			this._carregados++;
			this.loadIcon();			
		}

		public function removeIcon():void {
			if (!this.icoHolder)
				return;
			
			this.icoHolder.x 		= this.scrollMax;
			this.icoHolderLastPos	= this.scrollMax;
			this._carregados	= 0;
			this._x				= 0;
			
			while (this.icoHolder.numChildren > 0) {
				this.icoHolder.removeChildAt(0);
			}
		}

		/**
		 * Funcoes para criar ou resetar os tooltips dos incluidos pelo skin e que possuem 
		 * o parametro de tooltip no arquivo assets/info.xml.
		 * Nodes 
		 * <skin>
		 * 		<buttons>
		 */
		private function setTooltip(e:MouseEvent):void {
			this.toolTipController.setTooltip(e, "left", e.target.parent.x);
			this.hideBarDelay.stop();
			this.downBarDelay.stop();
		}
		
		private function resetTooltip(e:MouseEvent):void {
			this.toolTipController.resetTooltip(e);
			
			this.hideBarDelay.reset();
			this.hideBarDelay.start();
			
			this.downBarDelay.reset();
			this.downBarDelay.start();
		}
		
		/**
		 * Funcoes para mostar/esconder os tooltips dos botoes
		 * Botoes: info, help, infoBar.fechar, globe, closeInfo
		 */
		private function showTooltipIco(e:MouseEvent):void {
			Tweener.addTween(e.target._tooltip, { alpha:1, time:.5, transition:"linear" } );
		}
		
		private function hideTooltipIco(e:MouseEvent):void {
			Tweener.addTween(e.target._tooltip, { alpha:0, time:.5, transition:"linear" } );
		}
		
		/**
		 * Funcao para mostar os thumbs de capitulos do info
		 * geralmente usados nas animacoes
		 * Botoes: openBar
		 */
		private final function showChaptersBar(e:MouseEvent):void {
			if (!this.icoHolder)
				return;
				
			this.openBar.visible 	= false;
			this.prevCap.visible	= true;
			this.nextCap.visible	= true;			
			this.hideBarDelay.reset();
			this.hideBarDelay.start();
			this.icoHolder.visible	= true;
			this.icoHolder.x 		= this.icoHolderLastPos;
			this.hideBoard(e);
			Tweener.addTween(this.icoHolder, { alpha:1, time:.5, transition:"linear" } );			
		}
		
		/**
		 * Funcao para esconder os thumbs de capitulos do info, geralmente usados nas animacoes.
		 * Acionada quando o usuario posiciona o mouse fora do stage pelo tempo determinado no timer (hideBarDelay)
		 */
		private function hideBar(t:TimerEvent):void {
			if (!this.icoHolder)
				return;
				
			this.hideBarDelay.stop();
		
			if(this.prevCap)
				this.prevCap.visible	= false;
		
			if(this.nextCap)
				this.nextCap.visible	= false;
		
			if(this.openBar)
				this.openBar.visible	= true;
		
			Tweener.addTween(this.icoHolder, { alpha:0, time:.3, transition:"easeOutQuart", onComplete:function() { this.visible = false; } } );
		}
		
		/**
		 * Funcao para mostar/esconder (subir e descer) a barra de controles.
		 * Acionadas quando o usuario posiciona o mouse sobre o stage ou permanece com o mesmo fora do stage 
		 * pelo tempo determinado no timer (downBarDelay)
		 */
		private function downBar(t:TimerEvent):void {
			if (this.main.control && this.Play != null) {
				Mouse.hide();
				var newPos:Number	= this.main.control.y + (this.Play.height * 4);
				Tweener.addTween(this.main.control, { y:newPos, time:.5, transition:"easeInQuart" } );
				if(stage)
					stage.addEventListener(MouseEvent.MOUSE_MOVE, this.upBar);
			}
		}
		
		private function upBar(e:MouseEvent):void {
			Mouse.show();
			if(this.main.control)
				Tweener.addTween(this.main.control, { y:lastBarY, time:.5, transition:"easeOutQuart" } );
			
			stage.removeEventListener(MouseEvent.MOUSE_MOVE, this.upBar);
			this.downBarDelay.reset();
			
			this.downBarDelay.start();
		}
		
		/**
		 * Para os timers, soundTimer, hideBarDelay e downBarDelay
		 */
		private function stopTimer(e:MouseEvent):void {
			if(this.soundTimer)
				soundTimer.stop();
				
			this.hideBarDelay.stop();
			this.downBarDelay.stop();	
		}
		
		/**
		 * Reseta e inicia os timers, soundTimer, hideBarDelay e downBarDelay
		 */
		private function setTimer(e:MouseEvent):void {
			soundTimer.start();
			
			this.hideBarDelay.reset();
			this.hideBarDelay.start();
			
			this.downBarDelay.reset();
			this.downBarDelay.start();
		}
		
		/**
		 * Pausa o info
		 */
		public function _pause(e:MouseEvent = null):void {
			if(e){
				this.hideBoard(e);
				if(getQualifiedClassName(e.target) == "Pause")
					this._paused	= true;
			}
		
			this.Pause.visible	= false;
			this.Play.visible	= true;
			
			if(this.main._capituloAtualMc)
				this.main._capituloAtualMc.pauseMc();
			
			this.main.pauseCredito();
			this.pauseLettering();
		}
		
		/**
		 * Anima o info
		 */
		public function _play(e:MouseEvent):void {
			/**
			 * TODO
			 * Rever forma de recomecar sem ter q carregar novamente.
			 */
			if (this.finished) {
				trace("FIM")
			}else {
				if(e){
					this.hideBoard(e);
					if (this.galleryTab && this.galleryTab.status)
						this.closeAbaLateral(e);
					
					if(getQualifiedClassName(e.target) == "Play")
						this._paused	= false;
				}
				
				if(this.main._capituloAtualMc)
					this.main._capituloAtualMc.playMc();
				
				this.main.resumeCredito();
				this.resumeLettering();
				
				this.Pause.visible	= true;
				this.Play.visible	= false;
			}
		}
		
		private function showSoundBar(e:MouseEvent):void {
			stopTimer(e);
			if(this.somBar.currentFrame < 2)
				this.somBar.gotoAndPlay(2);
		}
		
		private function moveScrubber(e:MouseEvent):void {
			e.target.startDrag(true, new Rectangle(this.somBar.volumeBar.x, this.somBar.volumeBar.y, 0, this.somBar.volumeBar.height));
			this.addEventListener(MouseEvent.MOUSE_MOVE, this.setVolume);
		}
		
		private function setVolume(e:MouseEvent):void {
			stopTimer(e);
			var volum:Number	= setRange(this.somBar.scrubber.y - 11, 0, this.somBar.volumeBar.height, 100, 0) / 100;
			this.main.setVolume(Number(volum.toFixed(1)));
		}
		
		private function switchVolume(e:MouseEvent):void {
			if (this.main.volumeAtual == 1) {
				this.main.setVolume(0);
				this.som_on.visible 	= false;
				this.som_off.visible 	= true;
			}else {
				this.main.setVolume(1);
				this.som_on.visible 	= true;
				this.som_off.visible 	= false;
			}
			
			this.main.soundController.setVolume(this.main.volumeAtual);
		}
		
		private function hideSoundBar(e:TimerEvent):void {
			this.somBar.addEventListener(Event.ENTER_FRAME, rewind);
		}
		
		private function rewind(e:Event):void {
			if (e.target.currentFrame > 1) {
				e.target.prevFrame();
			}else {
				this.somBar.removeEventListener(Event.ENTER_FRAME, rewind);
			}
		}
		
		private function setRange(value:Number, inMin:Number, inMax:Number, outMin:Number, outMax:Number):Number{
            return (value - inMin) * (outMax - outMin) / (inMax - inMin) + outMin;
        }
				
		private function stopScrubber(e:MouseEvent):void {
			if(this.somBar)
				this.somBar.scrubber.stopDrag();
				
			this.removeEventListener(MouseEvent.MOUSE_MOVE, this.setVolume);	
		}
		
		public function setInteractive():void {
			this.next.mouseEnabled	= false;
			this.finished 			= true;
			
			this.Pause.visible		= false;
			this.Play.visible		= true;
			
			this.Play.mouseEnabled	= false;
			
			if (!this.interactive){
				this.main.soundController.playFromLibrary(interactiveSnd);
				this.main.soundController.setVolume(this.main.volumeAtual);
			}	
			
			this.interactive		= true;		
		}
		
		private function prevSession(e:MouseEvent):void {
			this.main.clearInterface();
			this.resetBox();
			this.hideBoard(e);
			this.Pause.visible		= true;
			this.Play.visible		= false;
			
			this.finished			= false;
			this.next.mouseEnabled	= true;
			
			this.Play.mouseEnabled	= true;
			
			this.main._capituloAtualMc.prevSession();
			if (this.main._capituloAtual == 0) {
				if (this.main._capituloAtualMc.sessaoAtual <= 0) {	
					this.main._capituloAtualMc.gotoSessao(0);
				}
			}
		}
		
		public function prevCapitulo():void {
			if (this.main._capituloAtual > 0  && !this.main.loading) {	
				this.main._capituloAtual--;
				this.main.clearInterface();
				this.resetBox();
				this.scrollBarRight();
				this.main.checkCapitulo(true);
			}
		}
		
		
		private function nextSession(e:MouseEvent):void {
			if(this.main._capituloAtualMc)
				this.main._capituloAtualMc.nextSession();
			
			this.hideBoard(e);
			this.resetBox();
			this.Pause.visible	= true;
			this.Play.visible	= false;
		}
		
		public function nextCapitulo():void {
			if (this.main._capituloAtual < (this.main.infoXML.tabs.children()[this.main.abaSelecionada].chapters.children().length() - 1) && !this.main.loading) {
				this.main._capituloAtual++;
				this.main.clearInterface();
				this.resetBox();
				this.scrollBarLeft();
				this.main.checkCapitulo();
			}
		}			
		
		public function gotoCapitulo(e:MouseEvent):void {
			this.setLastPos();
			this.main.clearInterface();
			this.main._capituloAtual	= e.target.capitulo;
			this.main.checkCapitulo();
		}
				
		public function scrollBarRight(e:MouseEvent = null):void {
			if (this.icoHolder && !Tweener.isTweening(this.icoHolder)) {
				var newPos:Number	= Math.ceil(this.icoHolder.x + this.scrollGap);
				if (newPos <= this.scrollMax) {
					if (e == null) {
						Tweener.addTween(this.icoHolder, { x:newPos, time:.5, transition:"linear", onComplete:setLastPos } );
					}else {
						Tweener.addTween(this.icoHolder, { x:newPos, time:.5, transition:"linear" } );
					}
				}
			}
		}
		
		public function scrollBarLeft(e:MouseEvent = null):void {
			if (this.icoHolder && !Tweener.isTweening(this.icoHolder)) {
				var newPos:Number	= Math.ceil(this.icoHolder.x - this.scrollGap);
				if(newPos >= this.scrollMin){
					if (e == null) {
						Tweener.addTween(this.icoHolder, { x:newPos, time:.5, transition:"linear", onComplete:setLastPos } );
					}else {
						Tweener.addTween(this.icoHolder, { x:newPos, time:.5, transition:"linear" } );
					}
				}
			}
		}
		
		private function voltarAt(e:MouseEvent):void{
			//dispatchEvent(new PrevCapituloEvent(PrevCapituloEvent.CAPITULO_PREV));
		}
		
		private function avancarAt(e:MouseEvent):void{
			this.atAvancar.visible 	= false;
			this.atVoltar.visible 	= false;
			this.nextSession(e);
		}
		
		private function setLastPos():void {
			this.icoHolderLastPos	= this.icoHolder.x;
		}
	}

}